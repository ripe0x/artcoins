// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {IArtCoinsExtension} from "../interfaces/IArtCoinsExtension.sol";
import {IArtCoinsFactory} from "../interfaces/IArtCoinsFactory.sol";
import {IArtCoinsAirdrop} from "./interfaces/IArtCoinsAirdrop.sol";

import {MerkleProof} from "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import {PoolKey} from "@uniswap/v4-core/src/types/PoolKey.sol";

import {Hashes} from "@openzeppelin/contracts/utils/cryptography/Hashes.sol";

import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {ReentrancyGuard} from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

/// @title ArtCoinsAirdrop
/// @notice Extension that escrows a token allocation and lets recipients claim per a Merkle root,
///         subject to a lockup and linear-vesting window.
contract ArtCoinsAirdrop is ReentrancyGuard, IArtCoinsAirdrop {
    /// @notice The factory authorized to call `receiveTokens`.
    address public immutable factory;
    /// @notice Per-token airdrop state.
    mapping(address token => AirdropV2 airdrop) public airdrops;

    /// @notice Minimum lockup duration before claims can begin. Set to 0 so
    ///         deployers who have verified the merkle root off-chain (e.g.
    ///         via the launcher UI's allowlist preview) can open claims in
    ///         the next block. The root still freezes after the first claim,
    ///         so a wrong root is unrecoverable — verify before deploying.
    uint256 public constant MIN_LOCKUP_DURATION = 0;
    /// @notice Window after vesting ends during which the admin can sweep unclaimed (14 days).
    uint256 public constant CLAIM_EXPIRATION_INTERVAL = 14 days;
    /// @notice After the lockup, time the admin may overwrite the merkle root if no claims have occurred.
    uint256 public constant ZERO_CLAIM_OVERWRITE_INTERVAL = 1 days;

    /// @dev Restricts a function to the factory.
    modifier onlyFactory() {
        if (msg.sender != factory) revert Unauthorized();
        _;
    }

    /// @param factory_ The factory.
    constructor(address factory_) {
        factory = factory_;
    }

    /// @notice Called by the factory to escrow the airdrop allocation.
    /// @param deploymentConfig Full deployment config.
    /// @param token The newly deployed token.
    /// @param extensionSupply Token amount allocated to this airdrop.
    /// @param extensionIndex Index of this extension in the deployment config.
    function receiveTokens(
        IArtCoinsFactory.DeploymentConfig calldata deploymentConfig,
        PoolKey memory,
        address token,
        uint256 extensionSupply,
        uint256 extensionIndex
    ) external payable nonReentrant onlyFactory {
        AirdropV2ExtensionData memory airdropData = abi.decode(
            deploymentConfig.extensionConfigs[extensionIndex].extensionData,
            (AirdropV2ExtensionData)
        );

        // check that we don't already have an airdrop for this token
        if (airdrops[token].merkleRoot != bytes32(0)) {
            revert AirdropAlreadyExists();
        }

        // ensure that the msgValue is zero
        if (deploymentConfig.extensionConfigs[extensionIndex].msgValue != 0 || msg.value != 0) {
            revert IArtCoinsExtension.InvalidMsgValue();
        }

        // check the vault percentage is not zero
        if (deploymentConfig.extensionConfigs[extensionIndex].extensionBps == 0) {
            revert InvalidAirdropPercentage();
        }

        // check that minimum lockup duration is met
        if (airdropData.lockupDuration < MIN_LOCKUP_DURATION) {
            revert AirdropLockupDurationTooShort();
        }

        // set the lockup, vesting, and claim end times
        airdrops[token].lockupEndTime = block.timestamp + airdropData.lockupDuration;
        airdrops[token].vestingEndTime =
            block.timestamp + airdropData.lockupDuration + airdropData.vestingDuration;
        airdrops[token].adminClaimTime = block.timestamp + airdropData.lockupDuration
            + airdropData.vestingDuration + CLAIM_EXPIRATION_INTERVAL;

        // set fields
        airdrops[token].admin = airdropData.admin;
        airdrops[token].merkleRoot = airdropData.merkleRoot;
        airdrops[token].totalClaimed = 0;
        airdrops[token].totalSupply = extensionSupply;

        // pull in token
        SafeERC20.safeTransferFrom(IERC20(token), msg.sender, address(this), extensionSupply);

        emit AirdropCreated({
            admin: airdropData.admin,
            token: token,
            merkleRoot: airdropData.merkleRoot,
            supply: extensionSupply,
            lockupDuration: airdropData.lockupDuration,
            vestingDuration: airdropData.vestingDuration
        });
    }

    /// @notice Transfers admin rights for a token's airdrop. Current admin only.
    /// @param token The token whose airdrop to update.
    /// @param newAdmin The new admin.
    function updateAdmin(address token, address newAdmin) external {
        if (msg.sender != airdrops[token].admin) revert Unauthorized();
        address oldAdmin = airdrops[token].admin;
        airdrops[token].admin = newAdmin;

        emit AirdropAdminUpdated(token, oldAdmin, newAdmin);
    }

    /// @notice Updates the merkle root if it is currently zero, or if the
    ///         `ZERO_CLAIM_OVERWRITE_INTERVAL` has passed without any claims. Admin only.
    /// @param token The token whose airdrop merkle root to update.
    /// @param newMerkleRoot The new merkle root.
    function updateMerkleRoot(address token, bytes32 newMerkleRoot) external {
        AirdropV2 storage airdrop = airdrops[token];

        // ensure that the msg sender is the admin
        if (msg.sender != airdrop.admin) revert Unauthorized();

        // ensure that the admin has not claimed
        if (airdrop.adminClaimed) revert AdminClaimed();

        // ensure that no claims have occurred
        if (airdrop.totalClaimed > 0) revert AirdropClaimsOccurred();

        // calculate if the zero claim overwrite interval has passed
        bool zeroClaimOverwriteIntervalPassed =
            block.timestamp > airdrop.lockupEndTime + ZERO_CLAIM_OVERWRITE_INTERVAL;

        // can change the merkle root if it is zero or if the zero claim overwrite interval has passed
        if (airdrop.merkleRoot != bytes32(0) && !zeroClaimOverwriteIntervalPassed) {
            revert UpdateMerkleRootNotAllowed();
        }

        // update the merkle root
        airdrop.merkleRoot = newMerkleRoot;

        emit AirdropMerkleRootUpdated(token, airdrop.merkleRoot, newMerkleRoot);
    }

    /// @notice Sweeps unclaimed tokens to a recipient after the claim expiration interval. Admin only.
    /// @param token The token whose airdrop to claim.
    /// @param recipient The address to receive the unclaimed balance.
    function adminClaim(address token, address recipient) external {
        AirdropV2 storage airdrop = airdrops[token];
        if (msg.sender != airdrop.admin) revert Unauthorized();
        if (block.timestamp < airdrop.adminClaimTime) revert ClaimNotEnded();
        if (airdrop.adminClaimed) revert AdminClaimed();

        // update the admin claimed flag
        airdrop.adminClaimed = true;

        // transfer the remaining balance to the recipient
        SafeERC20.safeTransfer(IERC20(token), recipient, airdrop.totalSupply - airdrop.totalClaimed);

        emit AirdropAdminClaimed(token, airdrop.totalSupply - airdrop.totalClaimed);
    }

    /// @notice Claims the currently vested portion of `recipient`'s airdrop allocation.
    /// @dev Verifies a Merkle proof of (`recipient`, `allocatedAmount`) against the airdrop's root.
    /// @param token The token whose airdrop to claim.
    /// @param recipient The recipient address (also the leaf identity).
    /// @param allocatedAmount The recipient's total allocated amount.
    /// @param proof Merkle proof of the leaf.
    function claim(
        address token,
        address recipient,
        uint256 allocatedAmount,
        bytes32[] calldata proof
    ) external nonReentrant {
        AirdropV2 storage airdrop = airdrops[token];

        // check that the airdrop exists
        if (airdrop.merkleRoot == bytes32(0)) {
            revert AirdropNotCreated();
        }

        // check if the admin has claimed
        if (airdrop.adminClaimed) revert AdminClaimed();

        // check that the lockup period has passed
        if (block.timestamp < airdrop.lockupEndTime) {
            revert AirdropNotUnlocked();
        }

        // check that the allocated amount is not zero
        if (allocatedAmount == 0) {
            revert ZeroClaim();
        }

        // check that the max claim amount has not been exceeded
        if (airdrop.totalClaimed >= airdrop.totalSupply) {
            revert TotalMaxClaimed();
        }

        // verify proof
        if (!MerkleProof.verifyCalldata(
                proof,
                airdrop.merkleRoot,
                keccak256(bytes.concat(keccak256(abi.encode(recipient, allocatedAmount))))
            )) {
            revert InvalidProof();
        }

        // calculate amount available to claim
        uint256 amountClaimed = airdrop.amountClaimed[recipient];
        if (amountClaimed >= allocatedAmount) {
            revert UserMaxClaimed();
        }

        // get total available amount unlocked
        uint256 claimableAmount = _getAmountClaimable(token, allocatedAmount, amountClaimed);

        // modulate down the amount available to claim if greater than available supply
        if (airdrop.totalClaimed + claimableAmount >= airdrop.totalSupply) {
            claimableAmount = airdrop.totalSupply - airdrop.totalClaimed;
        }

        if (claimableAmount == 0) {
            revert ZeroToClaim();
        }

        // update claimed amounts
        airdrop.amountClaimed[recipient] += claimableAmount;
        airdrop.totalClaimed += claimableAmount;

        // transfer tokens
        SafeERC20.safeTransfer(IERC20(token), recipient, claimableAmount);

        emit AirdropClaimed(
            token,
            recipient,
            airdrop.amountClaimed[recipient],
            allocatedAmount - airdrop.amountClaimed[recipient]
        );
    }

    /// @notice Returns the amount currently claimable for a recipient given their allocated amount.
    /// @dev Assumes the caller has a valid Merkle proof for `allocatedAmount`.
    /// @param token The token.
    /// @param recipient The recipient.
    /// @param allocatedAmount The recipient's total allocated amount.
    /// @return Amount claimable now.
    function amountAvailableToClaim(address token, address recipient, uint256 allocatedAmount)
        external
        view
        returns (uint256)
    {
        if (airdrops[token].merkleRoot == bytes32(0)) {
            revert AirdropNotCreated();
        }

        // check if the admin has claimed
        if (airdrops[token].adminClaimed) revert AdminClaimed();

        if (block.timestamp < airdrops[token].lockupEndTime) return 0;

        return _getAmountClaimable(token, allocatedAmount, airdrops[token].amountClaimed[recipient]);
    }

    function _getAmountClaimable(address token, uint256 allocatedAmount, uint256 totalUserClaimed)
        internal
        view
        returns (uint256)
    {
        if (block.timestamp >= airdrops[token].vestingEndTime) {
            // if the vesting period has passed, withdraw the remaining balance
            return allocatedAmount - totalUserClaimed;
        } else {
            // if the vesting period has not passed, calculate the amount to withdraw based on the
            // vesting period and how much has already been withdrawn
            uint256 totalAmountAvailable = allocatedAmount
                * (block.timestamp - airdrops[token].lockupEndTime)
                / (airdrops[token].vestingEndTime - airdrops[token].lockupEndTime);

            return totalAmountAvailable - totalUserClaimed;
        }
    }

    /// @notice ERC-165 introspection.
    /// @param interfaceId The interface id.
    /// @return True if supported.
    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return interfaceId == type(IArtCoinsExtension).interfaceId;
    }
}
